#!/usr/bin/perl
# Compares DNS query results to string.
# Author: Maik Glatki <maik.glatki@gmail.com>, 2014-201-20166
# Requires Net::DNS.

use strict;
use warnings;

# Core modules
require Getopt::Long;
require Tie::File;
require Pod::Usage;

# Extra modules
require Net::DNS;

sub printHelp() {
    Pod::Usage::pod2usage(1);
}

my $infile = '';
my $queries = {};
my $verbose = 0;

Getopt::Long::GetOptions(
    "i=s"       => \$infile,
    "A=s"       => \$queries->{A},
    "CNAME=s"   => \$queries->{CNAME},
    "MX=s"      => \$queries->{MX},
    # Extra
    "verbose"   => \$verbose,
);

# Read file
if ($infile eq '') {
    print STDERR "Please specify input file with -i parameter.\n";
    printHelp();
    exit 1;
}
if (! -f $infile) {
    print STDERR "Not a valid file: $infile.\n";
    printHelp();
    exit 2;
}
tie my @lines, 'Tie::File', $infile or die "Could not open file $infile: $!";

# Validate parameters.
my $num_params = 0;
my $mode = '';
for my $key (qw(A CNAME MX)) {
    if ($queries->{$key}) { $num_params++; $mode = $key; }
}

if (($num_params == 0) || ($num_params > 1)) {
    print STDERR "Wrong number of parameters given ($num_params). See help.\n";
    printHelp();
    exit 3;
}
my $query = $queries->{$mode};

# Check dns records
my $res = Net::DNS::Resolver->new;
if ($verbose) {
    print "Querying hosts: for $mode = $query\n";
}
my $exitcode = 0;
for my $host (@lines) {
    my $reply = $res->search($host);
    if ($reply) {
        my $entryset = 0;
        for my $rr ($reply->answer) {
            if ($rr->type eq $mode) {
                $entryset = 1;

                # Get result and compare
                my $res;
                if ($mode eq 'CNAME') {
                    $res = $rr->cname;
                } elsif ( $mode  eq 'A') {
                    $res = $rr->address;
                }
                if ($verbose) {
                    print "Result: $res\n";
                }
                if ($res ne $query) {
                    print "MISMATCH $host: $mode entry: $res.\n";
                    $exitcode = 4;
                }
            }
        }
        if ($entryset == 0) {
            print "MISMATCH: $host: no $mode entry!\n";
            $exitcode = 4;
        }
    } else {
        print "No reply for $host\n";
        $exitcode = 4;
    }
}
exit $exitcode;

__END__

=head1 NAME

Compares DNS entries on domains with a given set.

=head1 SYNOPSIS

  Usage: dnsbulkcompare -i FILE QUERIES

  QUERIES must be exactly one of the folowing options:
    -A               A entry query
    -CNAME           CNAME entry query
    -MX              MX entry query

  Other options:
   --help            Print this help message
   --verbose         Extra debug output.

=cut
